
begintemplate RoadmapWidget

    public show
    
    external isBaseOrNanoStart, isAstrocyteOrNeuron
    
    objref mainBox, baseOrNanoDeck
    
    // Ask user to choose the roadmap
    proc show() {
        strdef defaultChoice1HocCommand, defaultChoice2HocCommand
        defaultChoice1HocCommand = "isBaseOrNanoStart = 1"      // * Keep in sync
        defaultChoice2HocCommand = "isAstrocyteOrNeuron = 1"
        
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("Base or Nano")
            xlabel("Please choose whether to start with Base geometry or Nano geometry:")
            xradiobutton("Base", "baseOrNanoHandler(1)", 1)     // * Keep in sync
            xradiobutton("Nano", "baseOrNanoHandler(0)")
            xlabel("")
            xpanel()
            baseOrNanoDeck = new Deck()
            baseOrNanoDeck.intercept(1) {
                xpanel("Empty panel")
                xpanel()
                xpanel("Cell type")
                xlabel("Please choose type of the cell that will be imported and simulated:")
                xradiobutton("Astrocyte", defaultChoice2HocCommand, 1)
                xradiobutton("Neuron", "isAstrocyteOrNeuron = 0")
                xlabel("")
                xpanel()
            }
            baseOrNanoDeck.intercept(0)
            baseOrNanoDeck.map()
            baseOrNanoDeck.flip_to(1)                           // * Keep in sync
            xpanel("OK")
            xbutton("OK", "okHandler()")
            xpanel()
        }
        mainBox.dismiss_action("dismissHandler()")
        mainBox.intercept(0)
        mainBox.map("Roadmap", 550, 350, -1, -1)
        
        // In case if user does not touch radiobuttons, and just clicks "OK"
        execute(defaultChoice1HocCommand)
        execute(defaultChoice2HocCommand)
    }
    
    // Handler of "Base or Nano" radiobuttons
    proc baseOrNanoHandler() {
        isBaseOrNanoStart = $1
        baseOrNanoDeck.flip_to(isBaseOrNanoStart)
        if (!isBaseOrNanoStart) {
            isAstrocyteOrNeuron = -1
        }
    }
    
    // Handler of "OK" button for the widget
    proc okHandler() {
        // Close the widget
        mainBox.unmap()
        
        // Proceed with the next steps
        execute("roadmapChosenHandler()")
    }
    
    // Handler of "X" button for the widget
    proc dismissHandler() {
        // Close the widget
        mainBox.unmap()
    }

endtemplate RoadmapWidget
