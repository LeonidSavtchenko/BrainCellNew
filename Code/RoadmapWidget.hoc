
begintemplate RoadmapWidget

    public show
    
    external isBaseOrNanoStart, isAstrocyteOrNeuron
    external roadmapChosenHandler
    
    objref mainBox, baseOrNanoDeck
    
    
    proc init() {
        // Defaults
        isBaseOrNanoStart = 1
        isAstrocyteOrNeuron = 1
    }
    
    // Ask user to choose the roadmap
    proc show() {
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("Base or Nano")
            xlabel("Please choose whether to start with Base geometry or Nano geometry:")
            xradiobutton("Base", "baseOrNanoHandler(1)", isBaseOrNanoStart)
            xradiobutton("Nano", "baseOrNanoHandler(0)", !isBaseOrNanoStart)
            xlabel("")
            xpanel()
            baseOrNanoDeck = new Deck()
            baseOrNanoDeck.intercept(1)
            {
                xpanel("Empty panel")
                xpanel()
                xpanel("Cell type")
                xlabel("Please choose type of the cell that will be imported and simulated:")
                xradiobutton("Astrocyte", "isAstrocyteOrNeuron = 1", isAstrocyteOrNeuron)
                xradiobutton("Neuron", "isAstrocyteOrNeuron = 0", !isAstrocyteOrNeuron)
                xlabel("")
                xpanel()
            }
            baseOrNanoDeck.intercept(0)
            baseOrNanoDeck.map()
            baseOrNanoDeck.flip_to(1)
            xpanel("OK")
            xbutton("OK", "okHandler()")
            xpanel()
        }
        mainBox.dismiss_action("dismissHandler()")
        mainBox.intercept(0)
        mainBox.map("Roadmap", 550, 350, -1, -1)
    }
    
    // All next staff is private
    
    
    // Handler of "Base or Nano" radiobuttons
    proc baseOrNanoHandler() {
        isBaseOrNanoStart = $1
        baseOrNanoDeck.flip_to(isBaseOrNanoStart)
        if (!isBaseOrNanoStart) {
            isAstrocyteOrNeuron = -1
        }
    }
    
    // Handler of "OK" button for the widget
    proc okHandler() {
        // Close the widget
        dismissHandler()
        
        // Proceed with the next steps
        roadmapChosenHandler()
    }
    
    // Handler of "X" button for the widget
    proc dismissHandler() {
        // Close the widget
        mainBox.unmap()
    }

endtemplate RoadmapWidget

objref roadmapWidget
