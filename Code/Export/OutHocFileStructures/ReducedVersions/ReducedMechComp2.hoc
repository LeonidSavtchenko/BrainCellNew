
begintemplate ReducedMechComp

    public initHomogenBiophysics
    public name, list_ref, isMechInserted, mechStds
    external math
    
    strdef name
    objref list_ref
    double isMechInserted[1]
    objref mechStds[1][1]
    
    objref mechType
    numMechs = -1
    
    
    proc init() {
        name = $s1
        list_ref = $o2
        
        mechType = new MechanismType(0) // 0: "Distributed Membrane Mechanisms"
        numMechs = mechType.count
        
        double isMechInserted[numMechs]
        objref mechStds[numMechs][3]    // 0: "PARAMETER", 1: "ASSIGNED", 2: "STATE"
    }
    
    // Keep in sync with MechComp.applyHomogenChangesToDirtyMechs in the main program
    proc initHomogenBiophysics() { local morphologyMechIdx, diamVarTypeIdx, mechIdx, secIdx, varTypeIdx, diamValue localobj mechStd, nil
        strdef diamVarName
        
        morphologyMechIdx = 0
        diamVarTypeIdx = 0
        diamVarName = "diam"
        
        for mechIdx = 0, numMechs - 1 {
            if (!isMechInserted[mechIdx]) {
                continue
            }
            mechType.select(mechIdx)
            for secIdx = 0, list_ref.count() - 1 {
                list_ref.o(secIdx).sec {
                    mechType.make()
                    for varTypeIdx = 0, 2 {     // 0: "PARAMETER", 1: "ASSIGNED", 2: "STATE"
                        mechStd = mechStds[mechIdx][varTypeIdx]
                        if (mechStd == nil) {
                            continue
                        }
                        if (mechIdx != morphologyMechIdx || varTypeIdx != diamVarTypeIdx) {
                            // Here we assign NaN values to inhom vars, but they will be overwritten shortly downstream
                            mechStd.out()
                        } else {
                            diamValue = mechStd.get(diamVarName)
                            if (!math.isNaN(diamValue)) {
                                diam = diamValue
                            }
                        }
                    }
                }
            }
        }
    }
    
endtemplate ReducedMechComp
