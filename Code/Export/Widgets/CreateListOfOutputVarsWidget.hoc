
begintemplate CreateListOfOutputVarsWidget

    public show, dismissHandler
    
    external mwh, pyObj
    external addVarToGraph, unmapIfNotNil
    external eachItemInList
    
    objref mainBox
    objref varsList
    
    isRecordAndSaveWithAPCounts = -1
    isSaveToTxtOrBinFile = -1
    
    
    proc init() {
        varsList = new List()
        isRecordAndSaveWithAPCounts = 0
        isSaveToTxtOrBinFile = 1
    }
    
    proc show() { localobj graph, var, apcList
        strdef text
        
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("")
            xlabel("Please add all the vars you want to watch to the Graph below")
            xlabel("using the standard tool \"Plot what?\" and then click \"OK\".")
            xlabel("(You can use \"Delete\" to remove the vars added by mistake.)")
            xpanel()
            graph = new Graph()
            for eachItemInList(var, varsList) {
                addVarToGraph(graph, var.s)
            }
            graph.menu_remove("Crosshair")
            graph.menu_remove("Pick Vector")
            graph.menu_remove("Color/Brush")
            graph.menu_remove("View Axis")
            graph.menu_remove("New Axis")
            graph.menu_remove("View Box")
            graph.menu_remove("Erase Axis")
            graph.menu_remove("Axis Type")      // !! this one is not deleted for some reason
            graph.menu_remove("Keep Lines")
            graph.menu_remove("Family Label?")
            graph.menu_remove("Erase")
            graph.menu_remove("Move Text")
            graph.menu_remove("Change Text")
            xpanel("")
            xlabel("")
            apcList = new List("APCount")
            sprint(text, "Record and save the times with all existing APCount-s (%d detected)", apcList.count())
            xcheckbox(text, &isRecordAndSaveWithAPCounts, "recordAndSaveWithAPCountsCheckBoxHandler()")
            xlabel("")
            xlabel("Save to:")
            xradiobutton("TXT file", "isSaveToTxtOrBinFile = 1", isSaveToTxtOrBinFile == 1)
            xradiobutton("BIN file", "isSaveToTxtOrBinFile = 0", isSaveToTxtOrBinFile == 0)
            xlabel("")
            xbutton("OK", "okHandler()")
            xpanel()
        }
        mainBox.intercept(0)
        mainBox.dismiss_action("dismissHandler()")
        mainBox.map("Output variables", 150, 150, -1, -1)
        
        // Keep the var name in sync with GraphUtils.parseVarsFromTheGraph
        graph.save_name("someUniqueNameForTheParsedGraph")
    }
    
    proc dismissHandler() {
        unmapIfNotNil(mainBox)
    }
    
    // All next staff is private
    
    
    proc recordAndSaveWithAPCountsCheckBoxHandler() {
        strdef line1, line2, line3
        if (!isRecordAndSaveWithAPCounts) {
            return
        }
        line1 = "You can use the next standard widgets to add new or edit existing APCount-s:"
        line2 = "* Tools -> Point Processes -> Managers -> Point Manager"
        line3 = "* Tools -> Point Processes -> Managers -> Point Group"
        mwh.showMessageBox(line1, line2, line3, "APCount-s")
    }
    
    proc okHandler() {
        varsList.remove_all()
        pyObj.GraphUtils.parseVarsFromTheGraph(varsList)
        
        dismissHandler()
    }
    
endtemplate CreateListOfOutputVarsWidget
