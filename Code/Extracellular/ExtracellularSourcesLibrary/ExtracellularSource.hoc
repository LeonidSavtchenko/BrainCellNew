
// !!! remove the underscore in name once we get rid of "create ExtracellularSource"
begintemplate ExtracellularSource_

    public isEqual, checkForUnsupportedIon, packToVec
    public spcCatIdx, species, spatialInfo, temporalInfo, capacityInfo, seriesInfoOrNil
    public s
    
    external mwh, speciesLibrary
    external stringsEqual, codeContractViolation
    
    spcCatIdx = -1
    strdef species
    // !!! think about storing speciesIdx here
    
    objref spatialInfo, temporalInfo, capacityInfo, seriesInfoOrNil
    
    strdef s
    
    
    proc init() { local numArg localobj nil
        numArg = numarg()
        
        if (numArg < 5 || numArg > 6) {
            codeContractViolation()
        }
        
        spcCatIdx = $1      // !!! do I need this?
        species = $s2
        spatialInfo = $o3
        temporalInfo = $o4
        capacityInfo = $o5
        if (numArg == 6) {
            seriesInfoOrNil = $o6
        }
        
        if (spatialInfo.enumPointSphere == 0 && temporalInfo.enumStaticSwitchSpike == 1) {
            codeContractViolation()
        }
        // !!! add similar check for enumPointSphere+enumStaticSwitchSpike+capacityInfo
        
        if ((temporalInfo.isSeriesOrMinus1 != 1 && seriesInfoOrNil != nil) || (temporalInfo.isSeriesOrMinus1 == 1 && seriesInfoOrNil == nil)) {
            codeContractViolation()
        }
        
        prepareShownString()
    }
    
    func isEqual() { local cond localobj other
        other = $o1
        
        // !!! BUG: it must be illegal to create two sources with the same species, spatialInfo and temporalInfo independently on capacityInfo,
        //     to achieve this, we need to remove the last predicate in "cond" below,
        //     but after doing this, we will face other problem: user cannot click "Correct", change only capacityInfo, then click "Apply"
        
        // !!! maybe it must be illegal to create two sources with the only difference in either offsetTime, duration or seriesInfoOrNil
        
        cond = (spcCatIdx == other.spcCatIdx) && stringsEqual(species, other.species) && spatialInfo.isEqual(other.spatialInfo) && temporalInfo.isEqual(other.temporalInfo) && capacityInfo.isEqual(other.capacityInfo)
        if (!cond) {
            return 0
        }
        if (temporalInfo.isSeriesOrMinus1 != 1) {
            return 1
        }
        return seriesInfoOrNil.isEqual(other.seriesInfoOrNil)
    }
    
    // !!!! review when we call it and what it does, maybe use speciesLibrary.getSpeciesFlatIdx
    func checkForUnsupportedIon() { local spcFlatIdxOrMinus1, isUnsupported
        strdef msg
        // !!! maybe it would be enough to check spcCatIdx only
        spcFlatIdxOrMinus1 = speciesLibrary.getSpeciesFlatIdxOrMinus1(spcCatIdx, species)
        isUnsupported = (spcFlatIdxOrMinus1 == -1)
        if (isUnsupported) {
            sprint(msg, "\"%s\" species.", species)
            mwh.showNotImplementedWarning(msg)
        }
        return isUnsupported
    }
    
    // !!! this will be called from Extracellular module, but Export module will call other one
    obfunc packToVec() { local spcFlatIdxOrMinus1 localobj vec
        spcFlatIdxOrMinus1 = speciesLibrary.getSpeciesFlatIdxOrMinus1(spcCatIdx, species)
        // !!!! we need to skip the species present in JSON, but missing in MOD
        //      maybe use speciesLibrary.getSpeciesFlatIdx
        if (spcFlatIdxOrMinus1 == -1) {
            codeContractViolation()     // !!! maybe just skip this species
        }
        
        vec = new Vector()
        vec.append(spcFlatIdxOrMinus1, spatialInfo.packToVec(), temporalInfo.packToVec(), capacityInfo.packToVec())
        // !!!! no seriesInfoOrNil.packToVec() here by design
        return vec
    }
    
    // All next staff is private
    
    
    // !!! think about showing the spc cat (BI/NT/SP/EI/UN), the temporal info and the capacity info (maybe indicate the capacity just after the species name)
    proc prepareShownString() {
        strdef dynamicsName
        temporalInfo.getDynamicsName(dynamicsName)
        spatialInfo.prepareShownString(species, dynamicsName, temporalInfo.isSeriesOrMinus1, seriesInfoOrNil, s)
    }
    
endtemplate ExtracellularSource_
