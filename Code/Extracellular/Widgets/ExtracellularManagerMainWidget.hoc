
// !! some code dupl. with CreateListOfInputVarsWidget
begintemplate ExtracellularManagerMainWidget

    public show, onCreateHandler, onCorrectHandler, dismissHandler
    
    external ecsLibrary
    external isECSUnique, replaceItemInBrowsedListThenSelect, deleteSelectedItemFromBrowsedList, openDeck, closeDeck, unmapIfNotNil
    
    objref mainBox, ecsList
    objref oneECSWidget
    objref this
    
    firstColWidth = -1
    secondColWidth = -1
    slotHeight = -1
    
    
    proc init() {
        ecsList = ecsLibrary.ecsList
        
        // !!!
        firstColWidth = 345
        secondColWidth = 105
        slotHeight = 190
        
        oneECSWidget = new OneExtracellularSourceWidget(this)
    }
    
    proc show() { localobj hBox, deck
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("")
            xlabel("Extracellular sources:")
            xpanel()
            hBox = new HBox()
            hBox.intercept(1)
            {
                // It turns out, the usage of two Deck-s with just 1 card below
                // gives a nicer UI compared to other options (VBox, HBox or no-box)
                deck = openDeck()
                {
                    ecsList.browser("", "s")
                    if (ecsList.count() != 0) {
                        ecsList.select(0)
                    }
                    ecsList.accept_action("correctHandler()")   // Double click
                }
                closeDeck(deck, 0, firstColWidth, secondColWidth)
                deck = openDeck()
                {
                    xpanel("")
                    xbutton("Create new (default)", "createNewHandler()")
                    xbutton("Copy and edit", "copyAndEditHandler()")
                    xlabel("")
                    xbutton("Correct", "correctHandler()")
                    xbutton("Delete", "deleteHandler()")
                    xpanel()
                }
                closeDeck(deck, 0, secondColWidth, slotHeight)
            }
            hBox.intercept(0)
            hBox.map()
            xpanel("")
            xbutton("OK", "okHandler()")
            xpanel()
        }
        mainBox.intercept(0)
        mainBox.dismiss_action("dismissHandler()")
        mainBox.map("Manager of extracellular sources", 110, 150, -1, -1)
    }
    
    func onCreateHandler() { local numECSs localobj newECS
        newECS = $o1
        
        if (!isECSUnique(newECS, ecsList)) {
            return 0
        }
        
        ecsList.append(newECS)
        numECSs = ecsList.count()
        ecsList.select(numECSs - 1)
        
        return 1
    }
    
    func onCorrectHandler() { local oldSelIdx localobj newECS
        newECS = $o1
        // !!! maybe don't pass oldSelIdx here and just rely on the current selection
        //     (but need to close the child widget if user selects other src in this widget)
        oldSelIdx = $2
        
        if (!isECSUnique(newECS, ecsList)) {
            return 0
        }
        
        replaceItemInBrowsedListThenSelect(ecsList, newECS, oldSelIdx)
        
        return 1
    }
    
    proc dismissHandler() {
        dismissChild()
        unmapIfNotNil(mainBox)
    }
    
    // All next staff is private
    
    
    proc createNewHandler() {
        dismissChild()
        oneECSWidget.show()     // --> onCreateHandler
    }
    
    proc copyAndEditHandler() { local selIdxOrMinus1 localobj oldECS
        selIdxOrMinus1 = prologue4Show4NotNew(oldECS)
        if (selIdxOrMinus1 == -1) {
            return
        }
        
        oneECSWidget.show(oldECS)   // --> onCreateHandler
    }
    
    proc correctHandler() { local selIdxOrMinus1 localobj oldECS
        selIdxOrMinus1 = prologue4Show4NotNew(oldECS)
        if (selIdxOrMinus1 == -1) {
            return
        }
        
        oneECSWidget.show(oldECS, selIdxOrMinus1)   // --> onCorrectHandler
    }
    
    proc deleteHandler() {
        dismissChild()
        deleteSelectedItemFromBrowsedList(ecsList)
    }
    
    proc okHandler() {
        dismissHandler()
    }
    
    proc dismissChild() {
        oneECSWidget.dismissHandler()
    }
    
    func prologue4Show4NotNew() { local selIdx
        dismissChild()
        
        selIdx = ecsList.selected()
        if (selIdx == -1) {
            return -1
        }
        
        $o1 = ecsList.o(selIdx)
        return selIdx
    }
    
endtemplate ExtracellularManagerMainWidget
