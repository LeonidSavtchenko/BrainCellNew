
// !! get rid of this once Syn Manager is ready
// objref DistanceDistribution  // This was not used

begintemplate Synapse

    public source, netCon, target, single, dist, sec_ref
    public getPointProcess
    
    // !! uncomment here and below once it's exported
    // external codeContractViolation
    
    objref source, netCon, target, single, sec_ref
    
    dist = -1
    
    
    proc init() { local numArg
        numArg = numarg()
        if (numArg == 5) {
            source = $o1
            netCon = $o2
            target = $o3
            dist = $4
            sec_ref = $o5
        } else if (numArg == 3) {
            single = $o1
            dist = $2
            sec_ref = $o3
        } else {
            // codeContractViolation()
        }
    }
    
    obfunc getPointProcess() { local ppRoleIdx
        ppRoleIdx = $1  // 0: "Source PP", 1: "Target PP", 2: "Single PP"
        
        if (ppRoleIdx == 0) {
            return source
        } else if (ppRoleIdx == 1) {
            return target
        } else if (ppRoleIdx == 2) {
            return single
        } else {
            // codeContractViolation()
        }
    }
    
endtemplate Synapse

objref smAllSyns
smAllSyns = new List()

// Create and connect all synapses; the number of synapses is the same as the number of spines !!or less!!
// Inputs: SynapseLocationDendrite, weight, IntervalSynapticActivity, SwtichOn, noise (all taken from the top level)
proc CreateTheSynapticInput() { local synIdx, connectionPoint, dist localobj source, target, sec_ref, netCon, synapse
    strdef whereConnected
    
    // !! this is not used anywhere outside
    SynapseNumber = nanoDistal_ref.count()
    
    for synIdx = 0, SynapseNumber - 1 {
        source = new NetStim()
        
        if (SynapseLocationDendrite) {
            // Synapse is connected to the centre of spine_head
            connectionPoint = 0.5
            nanoDistal_ref.o(synIdx).sec {
                target = new Exp2Syn(connectionPoint)
                dist = distance(connectionPoint)
                sec_ref = new SectionRef()
            }
        } else {
            // Synapse is connected to the point on dendrite where spine_neck emerges
            nanoProximal_ref.o(synIdx).sec connectionPoint = parent_connection()
            nanoProximal_ref.o(synIdx).parent {
                target = new Exp2Syn(connectionPoint)
                dist = distance(connectionPoint)
                sec_ref = new SectionRef()
            }
        }
        
        // !! get rid of this once Syn Manager is ready
        // DistanceDistribution.x = dist
        // print "distance = ", dist
        // Conductance depended on distance. The formula is the same for all calculations
        // ConDistance = 0.5125 + dist * 0.65 / 300
        ConDistance = 1
        
        netCon = new NetCon(source, target, 0, 0, weight * ConDistance)
        source.start = 2
        source.interval = IntervalSynapticActivity
        source.number = SwtichOn
        source.noise = noise
        
        synapse = new Synapse(source, netCon, target, dist, sec_ref)
        smAllSyns.append(synapse)
    }
    
    if (SynapseLocationDendrite) {
        whereConnected = "spine_head(0.5)"
    } else {
        whereConnected = "dendrite(where spine_neck emerges)"
    }
    printf("SynapseNumber=%d; each connected to: %s\n", SynapseNumber, whereConnected)
}

// Inputs: SynapseNumber, smAllSyns, weight, IntervalSynapticActivity, SwtichOn, noise (all taken from the top level)
proc ChangeTheSynapticInput() { local synIdx localobj synapse
    // !! use the iterator once it's exported
    // for eachItemInList(synapse, smAllSyns) {
    for synIdx = 0, SynapseNumber - 1 {
        synapse = smAllSyns.o(synIdx)
        synapse.netCon.weight = weight
        synapse.netCon.pre(0).interval = IntervalSynapticActivity
        synapse.netCon.pre(0).number = SwtichOn
        synapse.netCon.pre(0).noise = noise
    }
}

// This proc will be called each time after neuron nanogeometry reseeding
proc Neuron_AddSynapses_reseed() {
    CreateTheSynapticInput()
    ChangeTheSynapticInput()
}

Neuron_AddSynapses_reseed()
