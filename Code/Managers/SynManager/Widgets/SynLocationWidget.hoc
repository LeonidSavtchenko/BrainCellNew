
begintemplate SynLocationWidget

    public show, getSynLocName, dismissHandler
    
    external mth, synGroup
    external createEmptyPanel, unmapIfNotNil, codeContractViolation
    external enumSynLoc
    
    objref mainBox, deck
    
    selEnumSynLoc = -1
    p_lastApplied = -1
    p = -1
    isReviewAfterApply = -1
    
    pMin = -1
    pMax = -1
    
    
    proc init() {
        p_lastApplied = 0.5
        
        isReviewAfterApply = 0
        
        pMin = 0
        pMax = 1
        
        variable_domain(&p, pMin, pMax)
    }
    
    proc show() { local synLocIdx
        strdef synLocName, handlerHocCommand, text
        
        selEnumSynLoc = enumSynLoc
        p = p_lastApplied
        
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("")
            xlabel("Synapse location:")
            for synLocIdx = 0, 2 {
                getSynLocName(synLocName, synLocIdx)
                sprint(handlerHocCommand, "synLocChangedHandler(%d)", synLocIdx)
                xradiobutton(synLocName, handlerHocCommand, selEnumSynLoc == synLocIdx)
            }
            xpanel()
            deck = new Deck()
            deck.intercept(1)
            {
                createEmptyPanel()
                xpanel("")
                xlabel("Bernoulli distribution:")
                for synLocIdx = 0, 1 {
                    getSynLocName(synLocName, synLocIdx)
                    sprint(text, "p = %d means \"all on %s\"", synLocIdx, synLocName)
                    xlabel(text)
                }
                xpvalue("p", &p, 1)
                xslider(&p, pMin, pMax)
                xpanel()
            }
            deck.intercept(0)
            // deck.flip_to(*)     // Will be done below in synLocChangedHandler
            deck.map()
            xpanel("")
            xlabel("Notes:")
            getSynLocName(synLocName, 1)
            sprint(text, "* When a synapse is moved to %s,", synLocName)
            xlabel(text)
            sprint(text, "  we set Spine Neck diam to %g (%s).", synGroup.witheredSpineNeckDiam, units(mth.diamVarName))
            xlabel(text)
            getSynLocName(synLocName, 0)
            sprint(text, "* When the synapse is moved back to %s,", synLocName)
            xlabel(text)
            xlabel("  we restore the old value of Spine Neck diam.")
            xbutton("Apply", "applyHandler()")
            xlabel("")
            xcheckbox("Review after \"Apply\"", &isReviewAfterApply)
            xlabel("       (this will open a new \"PointBrowser\")")
            xpanel()
        }
        mainBox.intercept(0)
        mainBox.dismiss_action("dismissHandler()")
        mainBox.map("Synapse location", 610, 65, -1, -1)
        
        synLocChangedHandler(selEnumSynLoc)
    }
    
    proc getSynLocName() { local numArg, synLocIdx
        numArg = numarg()
        
        if (numArg == 1) {
            synLocIdx = enumSynLoc
        } else if (numArg == 2) {
            synLocIdx = $2
        } else {
            codeContractViolation()
        }
        
        if (synLocIdx == 0) {
            $s1 = "Spine Head"
        } else if (synLocIdx == 1) {
            $s1 = "Dendrite (just under Spine Neck)"
        } else if (synLocIdx == 2) {
            $s1 = "Random"
        } else {
            codeContractViolation()
        }
    }
    
    proc dismissHandler() {
        unmapIfNotNil(mainBox)
    }
    
    // All next staff is private
    
    
    proc synLocChangedHandler() { local cardIdx
        selEnumSynLoc = $1
        cardIdx = (selEnumSynLoc == 2)
        deck.flip_to(cardIdx)
    }
    
    proc applyHandler() {
        enumSynLoc = selEnumSynLoc
        synGroup.applyChangesToLoc(p, isReviewAfterApply)
        if (selEnumSynLoc == 2) {
            p_lastApplied = p
        }
        isReviewAfterApply = 0
        dismissHandler()
    }
    
endtemplate SynLocationWidget

objref slw
slw = new SynLocationWidget()
