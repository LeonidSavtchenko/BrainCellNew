
begintemplate SynNCComp

    public name, isMechVarTypeInhom, mechStds
    public isMechVarInhom, prepareVarsEditorHeader, getDistRange, getSecList, markMechDirty, updateStatsCurve, applyInhomogeneity
    public isMechVarTypeInhom, mechStds, isMechDirty
    
    external mwh, mth, math
    external smAllSyns, synGroup
    external utils4FakeMech4NC
    external stringsEqual, codeContractViolation
    external eachItemInList
    
    strdef name
    
    double isMechVarTypeInhom[1][1]     // 0/1 flag
    objref mechStds[1][1]
    double isMechDirty[1]               // 0/1 flag
    
    
    proc init() { localobj mechStd
        name = "NetCon"
        
        double isMechVarTypeInhom[1][1]
        objref mechStds[1][1]
        double isMechDirty[1]
        
        mechStd = new FakeMechanismStandardForNetCon()
        if (synGroup.is3Or1PartInSynStruc()) {
            // It's a code contract that all the syns have homogeneous NetCon-s at the time of new SynNCComp-s construction
            mechStd.in(smAllSyns.o(0).netCon)
        }
        mechStds[0][0] = mechStd
        
        // !! search for inhomogeneities in the vars
    }
    
    // !! code dupl. with MechComp
    func isMechVarInhom() { local mechIdx, varTypeIdx, arrayIndex
        strdef varName
        
        mechIdx = $1
        varTypeIdx = $2
        varName = $s3
        arrayIndex = $4
        
        if (mechIdx != 0 || varTypeIdx != 0) {
            codeContractViolation()
        }
        
        return math.isNaN(mechStds[mechIdx][varTypeIdx].get(varName, arrayIndex))
    }
    
    proc prepareVarsEditorHeader() { local mechIdx, varType
        mechIdx = $1    // Just placeholders
        varType = $2    //
        
        if (mechIdx != 0 || varType != 1) {
            codeContractViolation()
        }
        
        sprint($s3, "All %s variables:", name)
    }
    
    proc getDistRange() {
        synGroup.getDistRange(&$&1, &$&2)
    }
    
    obfunc getSecList() {
        return synGroup.getSecList()
    }
    
    proc markMechDirty() { local mechIdx
        mechIdx = $1    // Just a placeholder
        
        if (mechIdx != 0) {
            codeContractViolation()
        }
        
        isMechDirty[mechIdx] = 1
    }
    
    proc updateStatsCurve() { local yMin, yMax, varType, arraySize, arrayIndex, y localobj graph, nil, syn
        strdef mechName, varName, varNameWithIndex
        
        graph = $o1
        yMin = $&2
        yMax = $&3
        mechName = $s4  // Just placeholders
        varType = $5    //
        varName = $s6
        arraySize = $7
        arrayIndex = $8
        
        if (smAllSyns.o(0).netCon == nil) {
            // The actual synapses don't use NetCon-s
            return
        }
        
        mth.getVarNameWithIndex(varName, arraySize, arrayIndex, varNameWithIndex)
        
        for synGroup.updateStatsCurveIter(syn, graph, &y, &yMin, &yMax) {
            y = utils4FakeMech4NC.getNetConVar(syn.netCon, varNameWithIndex)
        }
        
        $&2 = yMin
        $&3 = yMax
    }
    
    proc applyInhomogeneity() { local varType, arraySize, arrayIndex, y localobj distFuncHelper, syn
        strdef mechName, varName, varNameWithIndex
        
        mechName = $s1  // Just placeholders
        varType = $2    //
        varName = $s3
        arraySize = $4
        arrayIndex = $5
        distFuncHelper = $o6
        
        if (!stringsEqual(mechName, name) || varType != 1) {
            codeContractViolation()
        }
        
        mth.getVarNameWithIndex(varName, arraySize, arrayIndex, varNameWithIndex)
        
        mwh.showPleaseWaitBox("Applying inhomogeneity.")
        {
            for eachItemInList(syn, smAllSyns) {
                y = distFuncHelper.calculateAt(syn.dist)
                utils4FakeMech4NC.setNetConVar(syn.netCon, varNameWithIndex, y)
            }
        }
        mwh.hidePleaseWaitBox()
    }
    
endtemplate SynNCComp
