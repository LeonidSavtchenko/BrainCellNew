
begintemplate DistFuncGroupCHelper

    public selectedHandler, calculateAt, onNewSection, dismissHandler
    
    external mwh
    external selectDistFuncInputFile, loadPythonFile, codeContractViolation
    
    objref nestedHelper, parentWidget
    
    modelIdx = -1
    
    
    proc init() {
        nestedHelper = $o1
        parentWidget = $o2  // !! not used
        
        modelIdx = nestedHelper.modelIdx - 2
        
        if (modelIdx < 0 || modelIdx > 1) {
            codeContractViolation()
        }
    }
    
    func selectedHandler() { local isChosen, status
        strdef filePathName
        
        if (modelIdx == 0) {
            isChosen = selectDistFuncInputFile("HOC", "*.hoc", filePathName)
        } else {
            isChosen = selectDistFuncInputFile("Python", "*.py", filePathName)
        }
        if (!isChosen) {
            return 1
        }
        
        if (modelIdx == 0) {
            status = load_file(1, filePathName)
        } else {
            status = loadPythonFile(filePathName)
        }
        if (!status) {
            mwh.showWarningBox("Failed to load the file.")
            return 1
        }
        
        // !! ideally, we can analyze the loaded file and propose user a proper default function name
        
        return nestedHelper.selectedHandler(1)
    }
    
    func calculateAt() { local x
        x = $1
        return nestedHelper.calculateAt(x)
    }
    
    proc onNewSection() {
        // Nothing here
    }
    
    proc dismissHandler() {
        // Nothing here
    }
    
endtemplate DistFuncGroupCHelper
