
// Get the name of the currently accessed section (either object name or array name)
// Does not work if the section owned by a template!!
// $s1 (output) - The section name
proc getCurrentSecName() { local idx localobj strFunc
    strdef secName
    
    strFunc = new StringFunctions()
    
    idx = strFunc.head(secname(), "\\[", secName)
    if (idx == -1) {
        // This section is an object rather than array
        secName = secname()
    }
    
    $s1 = secName
}

// Get names of all created sections (either object name or array name)
// Does not work if there is a section owned by a template!!
// Returns List of String-s containing all unique names
obfunc getAllSectionNames() { local isFound, idx localobj secNames
    strdef secName
    
    secNames = new List()
    
    forall {
        getCurrentSecName(secName)
        
        // Check if we have found this name before
        // !! improve performace here using Set in Python
        isFound = 0
        for idx = 0, secNames.count() - 1 {
            if (stringsEqual(secName, secNames.o(idx).s)) {
                isFound = 1
                break
            }
        }
        
        if (!isFound) {
            secNames.append(new String(secName))
        }
    }
    
    return secNames
}
