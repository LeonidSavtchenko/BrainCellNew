
{ load_file("TopLevelVars.hoc") }
{ load_file("ErrWarnUtils.hoc") }
{ load_file("ListCopyUtils.hoc") }
{ load_file("BatchUtils.hoc") }
{ load_file("CheckUtils.hoc") }
{ load_file("DefaultSelectionUtils.hoc") }
{ load_file("SectionUtils.hoc") }
{ load_file("SectionRefUtils.hoc") }
{ load_file("PreviewUtils.hoc") }
{ load_file("OtherUtils.hoc") }
{ load_file("PythonUtilsForImport.hoc") }
{ load_file("Widgets_layer2.hoc") }
{ load_file("Import_layer2.hoc") }
{ load_file("Widgets_layer1.hoc") }
{ load_file("Import_layer1.hoc") }


// Import a file
// $s1 (optional) - HOC command to execute after successful import completion
// $s2 (optional) - Path to the input file; if omitted, then file browser will be shown
// Other input: isNanoCallbackReceived, isExternalSimStart, isAstrocyteOrNeuron (taken from the top level)
// Output (all initialized on the top level):
//      always: soma_ref, dendrite_ref (both List-s of SectionRef-s), NumberDendrites (scalar), usedNamesForSoma, usedNamesForDendrites (both List-s of String-s)
//      only if !isAstrocyteOrNeuron: axon_ref (List of SectionRef-s), usedNamesForAxon (List of String-s)
proc importHocOrSwcOrZipOrOtherFile() { local numArg, isCancel localobj pyEnumInFileType
    strdef filePathName, dllDirPath, fileExtLower
    
    if (isNanoCallbackReceived) {
        printImportFailedMsgAndRaiseError("Please restart and try again")
    }
    
    if (unix_mac_pc() != 3) {
        printImportFailedMsgAndRaiseError("The importer works only on Windows")
    }
    
    if (!nrnpython("")) {
        printImportFailedMsgAndRaiseError("Python is not available")
    }
    
    numArg = numarg()
    if (numArg > 2) {
        printImportFailedMsgAndRaiseError("0, 1 or 2 input arguments expected")
    }
    
    if (numArg >= 1) {
        importCompleteHandler = $s1
    } else {
        importCompleteHandler = ""
    }
    
    if (numArg == 2) {
        filePathName = $s2
    } else {
        if (!isExternalSimStart) {
            pyEnumInFileType = pyEnumInFileTypes.baseGeometryAny
        } else {
            pyEnumInFileType = pyEnumInFileTypes.baseGeometryHoc
        }
        isCancel = fbh.showLoadFileDialog(pyEnumInFileType, filePathName)
        if (isCancel) {
            return
        }
        if (isExternalSimStart) {
            dllDirPath = pyObj.getDirPath(filePathName)
            mechsDllUtils.loadMechsDllFromThisFolder(dllDirPath)
        }
    }
    
    // Prepare the temporary folder
    createOrCleanUpTempFolder("_Code/Import/temp_folder")
    
    getFileExtLowerCase(filePathName, fileExtLower)
    
    if (stringsEqual(fileExtLower, "hoc")) {
        importHocFile_layer1(filePathName, 0, 0, -1)
    } else if (stringsEqual(fileExtLower, "zip")) {
        importSwcFileFromZipArchive_layer1(filePathName)
    } else {
        importSwcOrOtherFile_layer1(filePathName)
    }
}
