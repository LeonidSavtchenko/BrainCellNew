
// Create or clean up the temporary folder
// $s1 - The temporary folder relative path and name
proc createOrCleanUpTempFolder() { local status
    strdef tempFolderRelPathName, tempFolderPathName, sysCommandTempl, sysCommand
    
    tempFolderRelPathName = $s1
    
    sprint(tempFolderPathName, "%s%s", getcwd(), tempFolderRelPathName)
    sysCommandTempl = "if not exist \"%s\" (mkdir \"%s\") else (rmdir /s /q \"%s\" && mkdir \"%s\")"
    sprint(sysCommand, sysCommandTempl, tempFolderPathName, tempFolderPathName, tempFolderPathName, tempFolderPathName)
    
    status = system(sysCommand)
    if (status) {
        printImportFailedMsgAndRaiseError("Cannot create or clean up the temporary folder")
    }
}

// Test the selected file with NLMorphologyConverter
// $s1 - Input file path and name
// $s2 - Output report file path and name
proc createReportFileWithNLMorphologyConverter() { local status
    strdef inFilePathName, outFilePathName, appPathNameTempl, appPathName, sysCommandTempl, sysCommand
    
    inFilePathName = $s1
    outFilePathName = $s2
    
    appPathNameTempl = "%s_Code/Import/3rdParty/NLMorphologyConverter/NLMorphologyConverter.exe"
    sprint(appPathName, appPathNameTempl, getcwd())
    sysCommandTempl = "call \"%s\" \"%s\" --report \"%s\""
    sprint(sysCommand, sysCommandTempl, appPathName, inFilePathName, outFilePathName)
    
    status = runSysOrPyCommandWithWaitBox(sysCommand, 1, "Analyzing")
    if (status) {
        // Just for sanity. Actually, NLMorphologyConverter does not return error codes, but rather prints a message
        printImportFailedMsgAndRaiseError("Cannot test the file with NLMorphologyConverter")
    }
}

// Convert the selected file to HOC with NLMorphologyConverter and save to the temporary folder;
// also, insert a default soma if missing
// $s1 - Input file path and name
// $2 - Soma size chosen by user (in um) or -1 if we won't insert a soma
// Other input: pyObj (taken from the top level)
// $s3 (Output) - The HOC file path and name
proc convertFileToHocWithNLMorphologyConverter() { local somaDiamAndLenOrMinusOne, status
    strdef inFilePathName, appPathNameTempl, appPathName, outFilePathNameTempl, outFilePathName, extraArgTempl, extraArg, sysCommandTempl, sysCommand
    
    inFilePathName = $s1
    somaDiamAndLenOrMinusOne = $2
    
    appPathNameTempl = "%s_Code/Import/3rdParty/NLMorphologyConverter/NLMorphologyConverter.exe"
    sprint(appPathName, appPathNameTempl, getcwd())
    outFilePathNameTempl = "%s_Code/Import/temp_folder/last_converted.hoc"
    sprint(outFilePathName, outFilePathNameTempl, getcwd())
    if (somaDiamAndLenOrMinusOne != -1) {
        extraArgTempl = "--forceCylinderSoma %s %s"
        // There is a typo in the manual for NLMorphologyConverter.exe: it claims to expect the radius as the first argument,
        // but actually it expects the diameter
        sprint(extraArg, extraArgTempl, pyObj.str(somaDiamAndLenOrMinusOne), pyObj.str(somaDiamAndLenOrMinusOne))
    } else {
        extraArg = ""
    }
    sysCommandTempl = "call \"%s\" \"%s\" \"%s\" NeuronHOC %s"
    sprint(sysCommand, sysCommandTempl, appPathName, inFilePathName, outFilePathName, extraArg)
    
    status = runSysOrPyCommandWithWaitBox(sysCommand, 1, "Converting")
    if (status) {
        // Just for sanity. Actually, NLMorphologyConverter does not return error codes, but rather prints a message and creates a minimal output file
        printImportFailedMsgAndRaiseError("Cannot convert the file to HOC format with NLMorphologyConverter")
    }
    
    $s3 = outFilePathName
}
