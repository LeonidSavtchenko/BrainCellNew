
begintemplate SimpleModelDistFuncHelper

    public selectedHandler, calculateAt, onJustBeforeFirstSection, onJustBeforeNewSection, onJustAfterLastSection, exportParams, importParams, dismissHandler
    public mainBox
    public parentWidget
    
    external codeContractViolation
    
    objref mainBox, parentWidget
    
    modelIdx = -1
    double params[1]
    
    numParams = -1
    
    
    proc init() { local numArg
    
        numParams = 6
        double params[numParams]
        
        numArg = numarg()
        
        if (numArg == 0) {
            // Empty by design (we are being created in the exported file, so will be inited in proc importParams shortly downstream)
        } else if (numArg == 1) {
            setModelIdx($1)
            
            if (modelIdx < 3) {
                params[1] = 0.1
                params[2] = 0.0005
            } else if (modelIdx == 3) {
                params[1] = 1
                params[3] = 1
                params[4] = 0.01
                params[5] = 2e-4
            } else {
                params[1] = 1
                params[2] = -0.01
            }
        } else {
            codeContractViolation()
        }
    }
    
    func selectedHandler() {
        mainBox = new VBox()
        mainBox.intercept(1)
        {
            xpanel("")
            if (modelIdx != 4) {
                xpvalue("a0", &params[0], 1, "valueChangedHandler()")   // !! if modelIdx == 0, then maybe make it equal to the initial value of the mech's variable
                if (modelIdx >= 1) {
                    xpvalue("a1", &params[1], 1, "valueChangedHandler()")
                    if (modelIdx >= 2) {
                        xpvalue("a2", &params[2], 1, "valueChangedHandler()")
                        if (modelIdx == 3) {
                            xpvalue("b0", &params[3], 1, "valueChangedHandler()")
                            xpvalue("b1", &params[4], 1, "valueChangedHandler()")
                            xpvalue("b2", &params[5], 1, "valueChangedHandler()")
                        }
                    }
                }
            } else {
                xpvalue("a", &params[0], 1, "valueChangedHandler()")
                xpvalue("b", &params[1], 1, "valueChangedHandler()")
                xpvalue("c", &params[2], 1, "valueChangedHandler()")
            }
            xpanel()
        }
        mainBox.intercept(0)
        mainBox.dismiss_action("dismissHandler()")
        mainBox.map("Distance func params", 210, 525, -1, -1)
        
        return 0
    }
    
    func calculateAt() { local x, xSq, y
        x = $1
        if (modelIdx != 4) {
            y = params[0]
            if (modelIdx >= 1) {
                y += params[1] * x
                if (modelIdx >= 2) {
                    xSq = x * x
                    y += params[2] * xSq
                    if (modelIdx == 3) {
                        y /= params[3] + params[4] * x + params[5] * xSq
                    }
                }
            }
            return y
        } else {
            return params[0] + params[1] * exp(params[2] * x)
        }
    }
    
    proc onJustBeforeFirstSection() {
        // Empty by design
    }
    
    proc onJustBeforeNewSection() {
        // Empty by design
    }
    
    proc onJustAfterLastSection() {
        // Empty by design
    }
    
    proc exportParams() { local idx localobj vecOfVals, listOfStrs
        vecOfVals = $o1
        listOfStrs = $o2    // Not used by design
        
        vecOfVals.resize(numParams + 1)
        vecOfVals.x(0) = modelIdx
        for idx = 0, numParams - 1 {
            vecOfVals.x(1 + idx) = params[idx]
        }
    }
    
    proc importParams() { local idx localobj vecOfVals, listOfStrs
        vecOfVals = $o1
        listOfStrs = $o2    // Not used by design
        
        if (vecOfVals.size() != numParams + 1 || listOfStrs.count() != 0) {
            codeContractViolation()
        }
        
        setModelIdx(vecOfVals.x(0))
        for idx = 0, numParams - 1 {
            params[idx] = vecOfVals.x(1 + idx)
        }
    }
    
    proc dismissHandler() {
        if (mainBox.ismapped()) {
            mainBox.unmap()
        }
    }
    
    // All next staff is private
    
    
    proc valueChangedHandler() {
        parentWidget.updateCurves()
    }
    
    proc setModelIdx() {
        modelIdx = $1
        if (modelIdx < 0 || modelIdx > 4) {
            codeContractViolation()
        }
    }
    
endtemplate SimpleModelDistFuncHelper
