
// !!! BUG: errors in custom proc-s "advance" (e.g. "object prefix is NULL") if user opens a simulation and immediately starts it
//     with RunControl or AltRunControl instead of the dedicated "Run" button; to fix it, we'll have to stick to AltRunControl
//     and integrate it with SimulationManager to be ready for this scenario

// !!!! maybe call .dismissHandler() for the current sim widget if user closes any its panel

objref simIdxToWidget[11]

begintemplate SimulationManager
    
    public createSimulationsPanel
    
    external isAstrocyteOrNeuron
    external InsertElectricMechanisms, InsertGlutamateMechanisms, InsertPotassiumMechanisms
    external stringEmpty, codeContractViolation
    external mmIcrHelper, beih
    
    external simIdxToWidget
    
    lastSimIdxOrMinus1 = -1
    
    double simIdxToLEDState[1]
    
    
    proc init() {
        simIdxToWidget[0] = new SimVoltageCA1Neuron()
        simIdxToWidget[1] = new SimGABADiffusionBasketCell()
        simIdxToWidget[2] = new SimFrapInCircleGeometry()           // !!! never called (hidden in UI)
        simIdxToWidget[3] = new SimFrapLine()
        simIdxToWidget[4] = new SimSpatialVoltageDistributions()
        simIdxToWidget[5] = new SimConstantElectricalSimulations()
        simIdxToWidget[6] = new SimFrequencyElectricalSimulation()  // !!! never called (hidden in UI)
        simIdxToWidget[7] = new SimCalciumDynamics()
        simIdxToWidget[8] = new SimCalciumWave()
        simIdxToWidget[9] = new SimGlutamate()
        simIdxToWidget[10] = new SimPotassium()
        
        double simIdxToLEDState[11]
        
        lastSimIdxOrMinus1 = -1
    }
    
    proc createSimulationsPanel() {
        xpanel("")
        xlabel("===================== Simulations =====================")
        xlabel("(Most sims below insert certain biophys mechs into the sections)")
        if (!isAstrocyteOrNeuron) {
            xlabel("---------------------------------------------------------------------------------------------------------")
            xlabel("Basic voltage simulation of CA1-neuron")
            xstatebutton("CA1-neuron voltage", &simIdxToLEDState[0], "simSelectedHandler(0)")
            xlabel("---------------------------------------------------------------------------------------------------------")
            xlabel("GABA diffusion simulation of basket cell interneuron")
            xstatebutton("Basket cell GABA diffusion", &simIdxToLEDState[1], "simSelectedHandler(1)")
        }
        //xlabel("---------------------------------------------------------------------------------------------------------")
        //xlabel("Simulation of FRAP experiment with round-spot bleaching")
        //xstatebutton("FRAP with round-spot bleaching", &simIdxToLEDState[2], "simSelectedHandler(2)")
        xlabel("---------------------------------------------------------------------------------------------------------")
        xlabel("Simulation of line-scan FRAP")
        xstatebutton("Line-scan FRAP", &simIdxToLEDState[3], "simSelectedHandler(3)")
        xlabel("---------------------------------------------------------------------------------------------------------")
        xlabel("Simulation of spatial distribution of voltage along dendrites") 
        xstatebutton("Membrane voltage distribution", &simIdxToLEDState[4], "simSelectedHandler(4)")
        xlabel("---------------------------------------------------------------------------------------------------------")
        xlabel("Parameters of somatic stimuli")
        xstatebutton("Constant electric stimuli", &simIdxToLEDState[5], "simSelectedHandler(5)")
        //xlabel("---------------------------------------------------------------------------------------------------------")
        //xlabel("Parameters of sinusoidal stimulations into soma")
        //xstatebutton("Variable electric stimuli", &simIdxToLEDState[6], "simSelectedHandler(6)")
        xlabel("---------------------------------------------------------------------------------------------------------")
        xlabel("Parameters for microscopic calcium dynamics")
        xstatebutton("Microscopic calcium dynamics", &simIdxToLEDState[7], "simSelectedHandler(7)")
        xlabel("---------------------------------------------------------------------------------------------------------")
        xlabel("Calcium Wave simulations")
        xstatebutton("Calcium Wave", &simIdxToLEDState[8], "simSelectedHandler(8)")
        xlabel("---------------------------------------------------------------------------------------------------------")
        xlabel("Cell model with Glutamate transporters")
        xstatebutton("Membrane biophysics with glutamate transport", &simIdxToLEDState[9], "simSelectedHandler(9)")
        xlabel("Cell model with potassium dynamics")
        xstatebutton("Dynamics of intra and extracellular K+", &simIdxToLEDState[10], "simSelectedHandler(10)")
        xpanel()
    }
    
    // All next staff is private
    
    
    proc simSelectedHandler() { local simIdx, isCancel, isRescanRequired localobj widget, nil
        strdef biophysJsonFileNameOrEmpty, hocCommand
        
        simIdx = $1
        
        if (simIdx == -1) {
            codeContractViolation()
        }
        
        if (lastSimIdxOrMinus1 != -1) {
            simIdxToWidget[lastSimIdxOrMinus1].dismissHandler()
            if (simIdx == lastSimIdxOrMinus1) {
                deployDefaultProcAdvance()
                lastSimIdxOrMinus1 = -1
                return
            } else {
                simIdxToLEDState[lastSimIdxOrMinus1] = 0
                doNotify()
            }
        }
        
        // !!! here we can call some more specialized proc because no need to have them imported or rescanned
        mmIcrHelper.makeSureMechCompsCreatedOrImportedAndRescanned()
        
        widget = simIdxToWidget[simIdx]
        
        isCancel = widget.check()
        if (isCancel) {
            simIdxToLEDState[simIdx] = 0
            return
        }
        
        // Import biophysics from JSON file
        biophysJsonFileNameOrEmpty = widget.biophysJsonFileNameOrEmpty
        if (!stringEmpty(biophysJsonFileNameOrEmpty)) {
            isCancel = beih.importForSim(biophysJsonFileNameOrEmpty)
            if (isCancel) {
                simIdxToLEDState[simIdx] = 0
                return
            }
        }
        
        // !!!! deprecated: remove this IF operator in favour of beih.importForSim (called just above) once we:
        //      (1) export/import density_GluTrans inhom model in the same way as for g_pas;
        //      (2) move the GLOBAL vars assigned in "MechanismSwitch_deprecated.hoc" to the loaded JSON file;
        //      (3?) create JSON files for the simulations hidden in UI (simIdx = 2 and 6)
        isRescanRequired = 0
        if (simIdx == 3) {
            InsertElectricMechanisms(0)
            isRescanRequired = 1
        } else if (simIdx == 7 || simIdx == 8) {
            InsertElectricMechanisms(1)
            isRescanRequired = 1
        } else if (simIdx == 9) {
            InsertGlutamateMechanisms()
            isRescanRequired = 1
        } else if (simIdx == 10) {
            InsertPotassiumMechanisms()
            isRescanRequired = 1
        }
        
        if (isRescanRequired && mmIcrHelper != nil) {   // !!! can be nil in tests
            mmIcrHelper.scheduleRescan(2)
        }
        
        if (widget.isCustomProcAdvance) {
            deployCustomProcAdvance(simIdx)
        } else {
            deployDefaultProcAdvance()
        }
        
        lastSimIdxOrMinus1 = simIdx
        
        widget.show()
    }
    
    proc deployDefaultProcAdvance() {
        execute("~proc advance() { fadvance() }")
    }
    
    proc deployCustomProcAdvance() { local simIdx
        strdef hocCommand
        simIdx = $1
        // !!!! for simplicity, we can call proc fadvance() in this generated HOC command rather than on the beginning of each proc widget.advance()
        sprint(hocCommand, "~proc advance() { simIdxToWidget[%d].advance() }", simIdx)
        execute(hocCommand)
    }
    
endtemplate SimulationManager

objref simManager
simManager = new SimulationManager()
